* Timing characterization for testing rom array in sky130 PDK
* use typical typical corner
.lib /software/PDKs/sky130A/libs.tech/xyce/sky130.lib.spice tt

.include simulation_file_4kB.sp

.option temp=27

Vvdd vdd 0 1.8 
Ven CS 0 1.8
Vgnd gnd 0 0 

* clock is set to pulse waveform: 
* PULSE(initial_value pulse_value, start_time, rise, fall, pulse_width, period, num_pulses(infinite if 0) )
Vclk clk 0 PULSE(0 1.8 200NS 0.1NS 0.1NS 200NS 400NS)

* In 4kB array wl_168 is a 1 and wl_169 is a 0

* 12 words per row so lower address set to 0b1011 -> sel 11
VA0 addr_0 0 1.8
VA1 addr_1 0 1.8
VA2 addr_2 0 0
VA3 addr_3 0 1.8


* Worst case read is the a 1 at the upper right corner of the array immediately after reading a 0
* 170 wordlines, low address WL_168 -> 0b10101000 upper: WL_169 -> 0b10101001
VA4 addr_4 0 PULSE(0 1.8 500NS 0.1NS 0.1NS 400NS 800NS)
VA5 addr_5 0 0
VA6 addr_6 0 0
VA7 addr_7 0 1.8
VA8 addr_8 0 0
VA9 addr_9 0 1.8
VA10 addr_10 0 0
VA11 addr_11 0 1.8


Xrom clk CS addr_0 addr_1 addr_2 addr_3 addr_4 addr_5 addr_6
+ addr_7 addr_8 addr_9 addr_10 addr_11 rom_out_0 rom_out_1 rom_out_2 rom_out_3 rom_out_4 rom_out_5
+ rom_out_6 rom_out_7 rom_out_8 rom_out_9 rom_out_10 rom_out_11 rom_out_12 rom_out_13
+ rom_out_14 rom_out_15 vdd gnd sram_rom_base_bank


.tran 0.001ns 1800ns

.MEASURE TRAN precharge_clk_delay TRIG V(clk)=0.9 RISE=4 TARG V(Xrom:precharge)=0.9 RISE=4
.MEASURE TRAN int_clk_delay TRIG V(clk)=0.9 RISE=4 TARG V(Xrom:clk_int)=0.9 RISE=4

.MEASURE TRAN precharge_skew TRIG V(Xrom:precharge)=0.9 RISE=4 TARG V(Xrom:WL_169)=0.9 FALL=2
.MEASURE TRAN row_decode_delay TRIG V(Xrom:clk_int)=0.9 RISE=4 TARG V(Xrom:WL_169)=0.9 FALL=2
.MEASURE TRAN wordline_buffer_delay TRIG V(Xrom:Xrom_row_decoder:wl_int169)=0.9 FALL=2 TARG V(Xrom:WL_169)=0.9 FALL=2

.MEASURE TRAN read_zero_delay TRIG V(clk)=0.9 RISE=4 TARG V(rom_out_15)=0.9 FALL=2
.MEASURE TRAN read_one_delay TRIG V(clk)=0.9 RISE=3 TARG V(rom_out_15)=0.9 RISE=2

.PRINT TRAN FORMAT=RAW FILE=char_4kB_functional.dat V(clk) V(Xrom:precharge) V(Xrom:clk_int)
+ V(addr_*) V(Xrom:Xrom_row_decoder:A_int_*) V(Xrom:Xrom_row_decoder:Ab_int_*)
+ V(rom_out_15) V(Xrom:bl_191) V(Xrom:bl_b_191) V(Xrom:bl_190) V(Xrom:bl_b_190) V(Xrom:rom_out_prebuf_15)
+ V(Xrom:wl_*) V(Xrom:Xrom_row_decoder:wl_int*)
+ V(Xrom:word_sel_*)
+ V(Xrom:Xrom_bit_array:bl_int_0_191) V(Xrom:Xrom_bit_array:bl_int_1_191) V(Xrom:Xrom_bit_array:bl_int_100_191) V(Xrom:Xrom_bit_array:bl_int_165_191)
+ V(Xrom:Xrom_bit_array:bl_int_0_190) V(Xrom:Xrom_bit_array:bl_int_1_190) V(Xrom:Xrom_bit_array:bl_int_100_190) V(Xrom:Xrom_bit_array:bl_int_165_190)